plugins {
	id 'jacoco'
	id 'java'
	id 'application'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.cs-25-2-team2'
version = '0.0.1-SNAPSHOT'
description = 'Restaurant Application project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

// Configure source sets to point to the actual Spring Boot app location
sourceSets {
	main {
		java {
			srcDirs = ['team2/sbm-restaurant-app/backend/src/main/java']
		}
		resources {
			srcDirs = ['team2/sbm-restaurant-app/backend/src/main/resources']
		}
	}
	test {
		java {
			srcDirs = ['team2/sbm-restaurant-app/backend/src/test/java']
		}
		resources {
			srcDirs = ['team2/sbm-restaurant-app/backend/src/test/resources']
		}
	}
}

// Configure the application plugin for Spring Boot
application {
    mainClass = 'com.cs_25_2_team2.RestaurantManagementApp.RestaurantManagementAppApplication'
}

// Task to run the CLI application
task runCLI(type: JavaExec) {
    group = 'application'
    description = 'Run the Restaurant Management CLI'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.cs_25_2_team2.RestaurantManagementApp.RestaurantManagementAppApplication'
    standardInput = System.in
}

// Alternative exec task for script compatibility
task exec(type: JavaExec) {
    group = 'application'
    description = 'Execute a Java class'
    if (project.hasProperty('mainClass')) {
        mainClass = project.property('mainClass')
    }
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.h2database:h2'
    implementation 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.postgresql:postgresql'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'
}

// Spring Boot main class configuration
springBoot {
	mainClass = 'com.cs_25_2_team2.RestaurantManagementApp.RestaurantManagementAppApplication'
}

test {
    // Use JUnit Platform for unit tests
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true // optional, useful for browsing coverage locally
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: ['**/RestaurantCLI*'])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: ['**/RestaurantCLI*'])
        }))
    }
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }
}

check {
    dependsOn jacocoTestCoverageVerification
}

spotless {
	java {
		// Specify the version of the formatter
		googleJavaFormat()
		
		// Import order configuration
		importOrder 'java', 'javax', 'org', 'com', ''
		
		// Remove unused imports
		removeUnusedImports()
		
		// Apply the formatting to all Java files in the project
		target 'src/*/java/**/*.java'
	}
}
